import java.text.SimpleDateFormat

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.2"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -fexceptions -pthread"
                // 要支持 'armeabi-v7a' 需开启 NO_ASM 宏
                // lame 编解码库的 lame_main.c 在方法 lame_main 中声明的
                // 局部变量占用内存较大，会导致申请的内存超过子线程栈的内存上限从而
                // 导致 SIGSEGV 错误，所以需要减小 MAX_NOGAP 的大小，具体见：
                // [issue2](https://github.com/WuFengXue/WeChatVoiceCodec/issues/2)
                cFlags "-DSTDC_HEADERS -DHAVE_LIMITS_H -DHAVE_MPGLIB -DNO_ASM -DMAX_NOGAP=50"
            }
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a','x86','x86_64'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue("string", "com_ms_module_wechat_voice_codec_name", rootProject.name)
            resValue("string", "com_ms_module_wechat_voice_codec_version", rootProject.ext._version)
            resValue("string", "com_ms_module_wechat_voice_codec_dependencies", "com.ms:$rootProject.name:$rootProject.ext._version")
            resValue("string", "com_ms_module_wechat_voice_codec_release_date", new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSS Z").format(new Date()).toString())


        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

allprojects {
    tasks.withType(Javadoc) {
        options {
            // 兼容中文字符
            encoding "UTF-8"
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/7/docs/api"
            // @date 注解为自定义注解
            tags "date:a:"
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}


apply from: "./maven_publish.gradle"